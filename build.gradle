plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

group 'blockproject.bpsl'
version 'PREVIEW_1'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'antlr'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.1"
    compile "org.antlr:antlr4-runtime:4.7.1"
}

generateGrammarSource {
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
        file("${buildDir}/generated-src/antlr/main").deleteDir()
    }

    arguments += ["-visitor", "-long-messages"]
}

sourceSets.main.java.srcDirs += "generated-src/main/java"

clean.doLast {
    file('generated-src').deleteDir()
    file('bin').deleteDir()
}

jar.enabled = false

shadowJar {
    manifest {
        attributes 'Main-Class': 'blockproject.bpsl.Main'
    }
    configurations = [project.configurations.compile, project.configurations.runtime]
    baseName = 'bpslcl'
    version = version
    archiveName = "${baseName}-${version}.${extension}"
}

assemble.dependsOn shadowJar
